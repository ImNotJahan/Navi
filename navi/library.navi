(set (> a b) (< b a))
(set (<= a b) (or (< a b) (= a b)))
(set (>= a b) (or (> a b) (= a b)))

(set (not p) (= p false))
(set (not= a b) (not (= a b)))

(set (null? var) (= (type_of var) null))
(set (pair? var) (= (type_of var) 'PAIR))

(set (positive? var) (> var 0))

(set (length list)
	(if (null? list)
		0
		(+ 1 (length (tail list)))))

(set (fold_left proc init list)
    (if (not (null? list))
        (fold_left proc
            (proc init (head list))
            (tail list))
    init))

(set (fold_right proc init list)
	(if (not (null? list))
		(proc (head list)
			(fold_right proc init (tail list)))
	init))

(set (list . items)
    (fold_right pair null items))

(set (reverse list)
    (fold_left (lambda (a x) (pair x a)) null list))

(set (append a b) (fold_right pair b a))

(set (hhead x) (head (head x)))
(set (hail x) (head (tail x)))

(expand (quasiquote x)
    (if (pair? x)
        (if (= (head x) 'unquote)
            (hail x)
            (if (= (if (pair? (head x)) (hhead x) null) 'unquote-splicing)
                (list 'append
                    (hail (head x))
                    (list 'quasiquote (tail x)))
                (list 'pair
                    (list 'quasiquote (head x))
                    (list 'quasiquote (tail x)))))
        (list 'quote x)))

(set (unary-map proc list)
    (fold_right (lambda (x rest) (pair (proc x) rest))
        null
        list))

(set (non_null? var) (not (null? var)))

(set (map proc . arg-lists)
	(if (non_null? (head arg-lists))
		(pair (apply proc (unary-map head arg-lists))
			(apply map (pair proc
				(unary-map tail arg-lists))))
		null))

(expand (++ var) ;(change ,var (+ ,var 1)))
(expand (-- var) ;(change ,var (- ,var 1)))

(expand (+= var value) ;(change ,var (+ ,var ,value)))
(expand (-= var value) ;(change ,var (- ,var ,value)))
(expand (*= var value) ;(change ,var (* ,var ,value)))
(expand (/= var value) ;(change ,var (/ ,var ,value)))

(expand (let defs . body)
    ;((lambda ,(map head defs) ,@body)
        ,@(map hail defs)))
		
(set (get list index) 
	(if (null? list)
		null
		(if (not= 0 index)
			(get (tail list) (- index 1))
			(head list))))

(set (in x list)
	(if (null? list)
		false
		(if (= (head list) x)
			true
			(in x (tail list)))))
	
(set (count n) (if (= n 0) 0 (+ 1 (count (- n 1)))))

(set (break) (error "EMPTY"))

(expand (do . body)
	;((lambda () ,@body)))

(expand (when p sugar . body)
	;(if ,p (do ,@body) null))
	
(set (newline) (say #\n))
	
(load "navi/strings.navi" "+ctos +string")
	
~ All the +strings makes this slow when an error does happen. Hopefully however, that's a rare case.
(set (check_type variable type from)
	(set (helper type) ~ Type error - from - expected type but got type of variable
		(error 
			"TYPE" 
			(+string 
				(+string "Expected " (string type)) 
				(+string " but got " (string (type_of variable)))) 
			from))
	
	~ if type is pair, check that type_of variable is in type
	(if (= (type_of type) 'pair) 
		(if (in (type_of variable) type) 
			null 
			(helper (type_of type)))
			
		(when (not= (type_of variable) type) do
			(helper type))))

(set (assert_that a) 
	(if a
		true
		(error "SYNTAX" "Test failed")))
		
(set (type? value type) (= (type_of value) type))

(expand (repeat times sugar . body)
	;(when (> ,times 0) do
		(do ,@body)
		(repeat (- ,times 1) times ,@body)))

(expand (while predicate sugar . body)
	;(when ,predicate do
		(do ,@body)
		(while ,predicate do ,@body)))
		
(expand (until predicate sugar . body)
	;(while (not ,predicate) do ,@body))
	
(expand (unless predicate sugar . body)
	;(when (not ,predicate) do ,@body))
	
(expand (pop list)
	;(change ,list (tail ,list)))

(expand (push item sugar list)
	;(change ,list (pair ,item ,list)))

(expand (for init predicate each sugar . body)
	(if (= predicate 'IN)
		;(for_each ,init in ,each do ,@body)
		;(do
			,init
			(while ,predicate do
				,@body
				,each))))

(expand (for_each item sugar items sugar . body)
	;(do
		(set items ,items)
		(while (non_null? items) do
			(set ,item (head items))
			,@body
			(pop items))))

(set (sayline a) (say a) (newline))

(set (// a b) (int (/ a b)))

(set (range start end)
	(let ((result null))
		(if (< start end)
			(for (set i start) (<= i end) (++ i) do
				(push i to result))
			(for (set i start) (>= i end) (-- i) do
				(push i to result)))
		(reverse result)))
		
(set (and p1 p2)
	(if p1 then_return
		p2 otherwise_return
		false))

(set (or p1 p2)
	(if p1 then_return true otherwise_return p2))